---
order: -34000
---
# 34.集合のビット表現 (was: BitDP)

[BitDPについて](https://qiita.com/masayoshi361/items/0be4bce77783b6013b34)

## 原理：集合のビット表現による数え上げ

要素数が20個くらいまでの集合に関して、その全ての部分集合について考える必要があるとき、
要素の数え上げ順を用いて部分集合を数え上げ、自然数に対応させるテクニック。

要素数 $N$ の集合の各要素に番号をつけて $U = \{a_0, a_1, a_{N-1}\}$ と呼ぶ。
このとき、任意の部分集合 $S$ にもまた番号 $n(S)$ を割り当てることができる。
この番号は、 $S$ に要素 $a_i$ が含まれるとき $n(S)$ のビット $i$ を $1$ とし、
含まれないとき $0$ とする。すると $0 \leq k < 2^N$ で、
空集合は $n(\empty) = 0$、全体集合は $n(U) = 2^N-1$ 番になる。

この番号付けの重要な性質は、ある集合の真部分集合に割り当てられる番号は必ず、
より小さい値になることである。

- $S \supset S' \Rightarrow n(S) > n(S')$

（ただし逆はいえない。集合の包含関係は半順序であるから。）  

これにより、ある全体集合の全ての部分集合について、何かの値を記録するとき、
整数添え字の配列にそれらを格納することができ、
またその値が、より小さな部分集合の値を参照して求められるとき、
その値は配列のより小さい添え字の位置にあることが保障されるため、
添え字の小さい方から計算していくことができる。（ほとんどネタバレ）

## 動的プログラミングへの応用

<!-- 以降、簡単のため、対象を $0$ から $N-1$ までの整数とする。-->

### 例題：ABC199E

[ABC199 E Permutation](https://atcoder.jp/contests/abc199/tasks/abc199_e)

#### 抽象的な攻略

$U = \{1,\dots,N\}$ の全ての部分集合 $S$ に対して、
「その要素の順列であって、条件を満たすものの個数 $c(S)$ 」を
求めることを考える。すると
$c(\empty) = 1$ であり、 $c(U)$ が求める値である。

ある集合 $S$ について $c(S)$ を求めることを考える。
すると、そこからいずれかの要素 $x \in S$ を除いた
部分集合 $S \backslash \{x\}$ で可能な順列の末尾に $x$ を
続けたものすべてを数えればよいので、

$$
c(S) = \sum_{x \in S} c(S \backslash \{x\})
$$

となるはずである。ただし、これらは条件を満たすものである必要がある。
そのため、 $|S| = X_i$ であるような制約 $(X_i,Y_i,Z_i)$ を満たすことを確認する。
これは $|\{a \in S \, | \, a \leq Y_i\}| \leq Z_i$ である。

ここで、 $|S| > X_i$ であるような制約についても、それらを満たさないものを除外する必要が
あるように思われるが、それらについては $|S'| = X_i$ である $c(S')$ を求める際に考慮が済んでいるので
その必要はない。以上をまとめると次の漸化式を得る。

$$
c(S) = \left \{
\begin{array}{ll}
1 & S=\empty のとき \\
\sum_{x \in S} c(S \backslash \{x\}) & \forall (X_i,Y_i,Z_i) \; |S| = X_i \Rightarrow |\{a \in S \, | \, a \leq Y_i\} | \leq Z_i のとき \\
0 & それ以外のとき
\end{array} \right .
$$

この漸化式を直接再帰的に計算して $c(U)$ を求めると同じ部分集合に関する計算が重複しまくるので、
メモ化により計算量を節約する。

Haskellでは配列 `Data.Array` だけでなく写像 `Data.Map` を用いてメモ化による動的プログラミングが実現でき、
また写像のキーに整数集合 `IntSet` が使えるので、上の式をほぼそのまま翻訳できる。

```haskell
import Data.Array
import Data.Maybe
import qualified Data.Map as M
import qualified Data.IntSet as IS

abc199e :: Int -> Int -> [[Int]] -> Int
abc199e n _m xyzs = c M.! IS.fromList [1..n]
  where
    conds = accumArray (flip (:)) [] (1,n) [(x,(y,z)) | (x:y:z:_) <- xyzs]
    c :: M.Map IS.IntSet Int
    c = M.fromList $
        map (f . IS.fromList . catMaybes) $
        sequence [[Nothing, Just i] | i <- [1..n]]
    f s
      | IS.null s = (s, 1)
      | isOK      = (s, sum [c M.! IS.delete x s | x <- IS.elems s])
      | otherwise = (s, 0)
      where
        isOK = and [z >= IS.size (fst $ IS.split (succ y) s) | (y,z) <- conds ! IS.size s]
```

この実装は数学的表現とよく対応して美しいが、[計算時間的には無理がある](https://atcoder.jp/contests/abc199/submissions/29782691)。

#### 集合のビット表現による効率化

そこで、直接 `IntSet` をキーにした `Map` を作る代わりに、
ビット表現をキーにした整数添え字配列で実現する。
$\{1,\dots,Y\}$ の表現が $2^Y - 1$ になるように、1ずらして考えている。

```haskell
import Data.Array
import Data.Bits

abc199e :: Int -> Int -> [[Int]] -> Int
abc199e n m xyzs = c ! pow2n1
  where
    pow2n1 = pred $ shiftL 1 n
    conds = accumArray (flip (:)) [] (1,n) [(x,(y,z)) | (x:y:z:_) <- xyzs]
    c :: Array Int Int
    c = listArray (0,pow2n1) $ map f [0..pow2n1]
    f 0        = 1
    f s | isOK = sum [c ! clearBit s x1 | x1 <- [0..pred n], testBit s x1]
        | True = 0
      where
        isOK = and [z >= popCount (s .&. pred (shiftL 1 y)) | (y,z) <- conds ! popCount s]
```

要素が含まれるかどうかは `testBit` で判定できる、
1要素を取り除いた集合の表現は `clearBit` で作れる、
共通部分はビットごとの論理積で求められる、
集合の要素数が `popCount` で数えられる、
という対応関係が興味深い。
[このコードはACする。](https://atcoder.jp/contests/abc199/submissions/29771345)

### パターン化

上のABC199Eに似て、
[ABC041 D 徒競走](https://atcoder.jp/contests/abc041/tasks/abc041_d)もまた、
順列の中で、指定した条件を満たすものの個数を数える問題である。
ABC199Eでは、今考えている集合 $S$ が条件を満たすものか否か、だけを考えたが、
ABC041Dでは、一つ小さい集合 $S \backslash \{x\}$ の順列の末尾に $x$ を追加したとき、
条件に反しないかどうか、を個別に判定して集めてくる形になる。

部分集合 $S$ の順列で条件を満たすものの個数を $c(S)$ 、
$S$ から要素 $x$ を減らした集合に対する $c$ について、それらの順列の末尾に $x$ を追加した順列を
どれだけ数えるかを決める関数を $g$ とすると、
漸化式は以下のようになる。（cはcountのc）

$$
c(S) = \left \{
\begin{array}{ll}
1 & S = \empty のとき \\
\sum_{x \in S} g(c(S \, \backslash \, \{x\}), S \, \backslash \, \{x\}, x) & それ以外のとき
\end{array}
\right .
$$

ABC041Dでは、条件で $x$ よりも後に来るべきと指定されている要素が $S \backslash \{x\}$ に含まれるとき
$g = 0$, さもなくば $g = c(S \backslash \{x\})$ とすればよい。

例題がないが、順列に関するコストを最小化する問題も同様に対応できる。
$g$ も上と同様な情報から最小コストを算出する関数として、
漸化式は以下のようになる。（cはcostのc）

$$
c(S) = \left \{
\begin{array}{ll}
\infty & S = \empty のとき \\
\min_{x \in S} \, g(c(S \, \backslash \, \{x\}), S \, \backslash \, \{x\}, x) & それ以外のとき
\end{array}
\right .
$$

となる。

この二つの漸化式は、
空集合に対する値（ $1$ か $\infty$ か）、
要素をひとつ減らした集合に対する結果 $g$ を集める方法（ $\sum$ か $\min$ か）だけが異なるので、
これらをパラメータとして統合できる。
ABC199Eの場合も含めてこれらを一般化すると、次のHaskellプログラムを得る。
引数 `base` が空集合に対する値、
`accum` が $g$ を集める方法、
`g` が $g$ に対応する。

```haskell
import Data.Bits

type BitSet = Int

bitDP :: a -> (BitSet -> [a] -> a) -> (a -> BitSet -> Int -> a) -> Int -> a
bitDP base accum g n = arr ! all1
  where
    all1 = 2^n - 1
    arr = listArray (0,all1) $ map f [0..all1]
    f 0 = base
    f k = accum k [g (arr ! k1) k1 i | i <- [0..pred n], testBit k i, let k1 = clearBit k i]
```

さらにMutable Vectorに直す。

```haskell
import Data.Bits
import qualified Data.Vector.Unboxed.Mutable as MUV
import Control.Monad
import Control.Monad.ST

type BitSet = Int

bitDP :: a -> (BitSet -> [a] -> a) -> (a -> BitSet -> Int -> a) -> Int -> a
bitDP base accum g n = runST action
  where
    pow2n = 2^n
    pow2n1 = pred pow2n
    action :: ST s Int
    action = do
      v <- MUV.new pow2n
      MUV.write v 0 base
      forM_ [1..pow2n1] $ (\k -> do
        gs <- forM [i | i <- [0..pred n], testBit k i] $ (\i -> do
            let k1 = clearBit k i
            fk1 <- MUV.read v k1
            return $ g fk1 k1 i
            )
        MUV.write v k $ accum k gs
        )
      MUV.read v pow2n1
```

このような、順列に関する問題に対する計算コストは、
素朴に生成と評価を行うと $O(N!)$ であるところ、
$2^N$ 要素の配列の内容を順に求めていく形に変形され、
$g$ の計算を $O(1)$ と仮定すると $O(2^N)$ となる。

どちらもずいぶん大きな値に見えるが、階乗の方が大きい。

$$
\begin{array}{llll}
N! & = 1 \times 2 \times 3 \times \dots \times (N-1) & \times & N \\
2^N & = 2 \times 2 \times 2 \times \dots \times 2    & \times & 2
\end{array}
$$

### 巡回セールスマン問題

巡回セールスマン問題を解くには、
都市の部分集合について、それらを巡回する経路でコストの最も安いものをそれぞれ求める。
つまり順列のコスト最小化をする。

ただし、ある都市集合 $S$ のコストを求める漸化式が上のパターンに当てはまらない。
都市を一つ $x$ 除いた集合のコスト $c(S \backslash \{x\})$ と追加する都市 $x$ からだけでは $c(S)$ が求められない。  
コスト関数の引数を、都市集合に加えて、順列の末尾がどこかも指定する。
つまり、 $c(S,y)$ を都市集合 $S$ を巡回して都市 $y$ で終わる経路の最も安いコストとする。  
すると、都市間のコストを $\textrm{cost}$ で表すとして、積み上げは次のようにできる。
（ $x \in S$ とする）

$$
c(S,x) = \min_{y \in S \backslash \{x\}} \big ( c(S \backslash \{x\}, y) + \textrm{cost}(y,x) \big )
$$

漸化式は以下のようにまとめられる。
（出発点の制約など、問題によって一部変化するので、一つの例として）

$$
c(S,x) = \left \{
\begin{array}{ll}
0 & S = \{x\} のとき \\
\infty & x \not\in S のとき \\
\min_y \big ( c(S \backslash \{x\}, y) + \textrm{cost}(y,x) \big ) & それ以外のとき
\end{array}
\right .
$$

この2引数関数 $c(S,x)$ を二次元配列にメモ化すればよい。

#### 発展

上の $c(S,x)$ は結局、 $x \in S$ の範囲をまとめて調べる使い方しかされない。
そこで、この関数をカリー化する、あるいは $S$ に対して、
末尾の都市からコストへの写像（または一次元配列）を対応づける一次元配列とみなす。

そうすると、上の `bitDP` のインスタンスにできそうにも見える。

### 順列でない例

[ABC142 E Get Everything](https://atcoder.jp/contests/abc142/tasks/abc142_e) は、
全ての宝箱の組み合わせに関して、それらを全て開ける鍵の費用の最安値を状態とする。

```haskell
State :: Array BitSet Int
```

最初、鍵をひとつも選んでいないとき、どの箱も開けられないことを、不可能を表す大きな値で表す。

```haskell
initialState :: State
initialState = listArray (0,2^n-1) $ 0 : replicate (2^n-1) (div maxBound 2)
```

一つの鍵を手に取ったとき、それも使うことで、より安く開けられる宝箱のパターンを全て更新する。  
パターン `s` を開ける既知の最小コストが `x` であり、
鍵がコスト `a` でパターン `cs` を開けられるとき、
パターン `s .|. cs` をコスト `x + a` で開けられる。

```haskell
step :: State -> (Int, BitSet) -> State
step st (a, cs) = accum min st [(s .|. cs, x + a) | (s,x) <- assocs st]
```

全ての鍵を試したあと、全て開けられるバターンの最安値が答えである。

```haskell
answer = foldl step initialState acss ! (2^n-1)
```

この例は、集合のビット表現を用いているし、動的プログラミングも用いているが、
動的プログラミングの進行方向と集合のビット表現は無関係で、順列も関係しない。
私見だが「bitDP」という言葉の範囲からは少し外れた例に見える。

## 関連問題

- [ABC180 E Traveling Salesman among Aerial Cities](https://atcoder.jp/contests/abc180/tasks/abc180_e) - [ACコード](https://atcoder.jp/contests/abc180/submissions/29618221)  
Vector化したが、Unboxできなかったせいか[遅くなった](https://atcoder.jp/contests/abc180/submissions/29621326)
- [ABC142 E Get Everything](https://atcoder.jp/contests/abc142/tasks/abc142_e) - [ACコード](https://atcoder.jp/contests/abc142/submissions/29616204)
- [ABC199 E Permutation](https://atcoder.jp/contests/abc199/tasks/abc199_e) - [ACコード](https://atcoder.jp/contests/abc199/submissions/29771345)
- [ABC041 D 徒競走](https://abc041.contest.atcoder.jp/tasks/abc041_d) - [ACコード](https://atcoder.jp/contests/abc041/submissions/29606819)
- [第4回 ドワンゴからの挑戦状 予選 D ディスクの節約](https://atcoder.jp/contests/dwacon2018-prelims/tasks/dwacon2018_prelims_d)
