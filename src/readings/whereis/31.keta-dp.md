---
order: -31000
---

# 31.桁DP

「 $1$ 以上 $N \leq 10^{10000}$ 以下の整数のうち○○であるものの個数を求めよ。」
というタイプの問題のときの考え方、という話。


- [桁DPに入門してみた](https://zehnpaard.hatenablog.com/entry/2018/06/24/215607)
- [桁DP入門](http://web.archive.org/web/20190103133620/https://pekempey.hatenablog.com/entry/2015/12/09/000603)

前者は後者の写経のようなので、同じアプローチでやってみる。

## 例題

> A以下の非負整数のうち「3の倍数または3の付く」かつ「8の倍数でない」数がいくつあるか。

を次のように段階に分けて考えていく。

1. A以下の非負整数の総数を求める
2. A以下の非負整数のうち、3の付く数の総数を求める
3. A以下の非負整数のうち、「3が付くまたは3の倍数」の数の総数を求める
4. A以下の非負整数のうち、「3が付くまたは3の倍数」かつ「8の倍数でない」数の総数を求める

### ステップ1

A以下の非負整数の総数を求める。  
（A以下の非負整数の総数、って0を含めてA+1以外の何物でもない、とか言わない。）

例として A = 4275631 とする。

最上位桁から順に、条件を満たす場合を作っていくことを考える。  
最上位桁には何をいれてよいかというと、0～4の数字なら「A以下」に反さない。
5以上の数字を入れてしまうと、A以下の数を作ることは不可能。

```
A = 4 2 7 5 6 3 1
    0 ? ? ? ? ? ? OK
    1 ? ? ? ? ? ? OK
    :
    4 ? ? ? ? ? ? OK
    5 ? ? ? ? ? ? XXX NG XXX
    :
    9 ? ? ? ? ? ? XXX NG XXX
```

ここで最上位桁に4を選んだ場合について、次の桁をどうしていいか考える。  
すると次に許されるのは0～2である。

```
A = 4 2 7 5 6 3 1
    4 0 ? ? ? ? ? OK
    4 1 ? ? ? ? ? OK
    4 2 ? ? ? ? ? OK
    4 3 ? ? ? ? ? XXX NG XXX
    4 :
    4 9 ? ? ? ? ? XXX NG XXX
```

一方、最上位桁に2を選んだ場合はどうだろうか。  
このときは、何を選んでもAを超える心配はない。

```
A = 4 2 7 5 6 3 1
    2 0 ? ? ? ? ? OK
    2 : ? ? ? ? ? OK
    2 9 ? ? ? ? ? OK
```

さらに下の桁まで確定させていった場合も同様で、

- それまでにどこか1箇所でも、Aよりも小さい数字を選んだ桁がある場合、A未満は確定しているので桁の選択は自由
- ずっとAとぴったり同じ数字を選び続けている場合、桁の選択はAと同じまで、またこの桁でAより小さい数字を選んだ場合も、A未満が今回で確定する

となる。

Aが上位から桁ごとの数のリストで与えられるとし、1桁ずつDPで場合の数を数えるプログラムを考える。  
DP配列の添え字は、A未満が確定しているとき `True` な `Bool` とする。  
確定済みならば、今回の桁に関して0から9のどの数字を選んでもよい。  
未確定ならば、Aiまでの数字しか選べない。  
しかし、再度Aiと同じ数字を選んだ場合のみが、新たな未確定の場合となり、
この桁でAiより小さい数字を選んだ場合は全て、確定済みの方に足しこむ。

```haskell
import Data.Array

step1 :: [Int] -> Integer
step1 as = ans
  where
    bnds :: (False,True)                   -- A未満が確定
    initial :: Array Bool Integer
    initial = listArray bnds [1,0]         -- 1桁も確定していない段階では、未確定の場合が1あるだけ
    final = foldl step initial as          -- 桁ごとに配列を更新していく
    step arr ai = accumArray (+) 0 bnds
      [ ( less ||                          -- A未満が確定しているか、
          d < ai                           -- またはまさに今回確定したなら確定済み
        , val)                             -- に足しこむ
      | (less, val) <- assocs arr          -- 全ての場合について考える
      , val > 0                            -- 足しこむ必要がない場合は飛ばす
      , let ub = if less then 9 else ai    -- 使える数字の上限
      , d <- [0..ub]                       -- 次に使える全ての数字について考える
      ]
    ans = sum [val | (_,val) <- assocs final] -- 全ての場合の総和
```

```
> step1 [4,2,7,5,6,3,1]
4275632
```

### ステップ2

A以下の非負整数のうち、3の付く数の総数を求める。

DP配列の次元を追加し「数字の3を選んだことがあるとき `True`」かそうでないかで場合分けして数える。

```haskell
step2 :: [Int] -> Integer
step2 as = sum $ elems final
  where
    bnds = ((False,False),(True,True))     -- A未満が確定/3を含む
    initial :: Array (Bool,Bool) Integer
    initial = listArray bnds $ 1 : replicate 3 0
    final = foldl step initial as
    step arr ai = accumArray (+) 0 bnds
      [ (( less || d < ai
         , has3 || d == 3)                -- 3を含むか、今回使ったならTrueに足しこむ
        , val)
      | ((less,has3),val) <- assocs arr
      , val > 0
      , d <- [0..if less then 9 else ai]
      ]
    ans = sum [val | ((_,True),val) <- assocs final] -- 3を含む場合の総和
```

```
> step2 [4,2,7,5,6,3,1]
2520497
(0.00 secs)
> length [() | i <- [0..4275631], elem '3' $ show i]
2520497
(3.47 secs)
```

### ステップ3

A以下の非負整数のうち、「3が付くまたは3の倍数」の数の総数を求める

3の倍数は、桁ごとの数字の和が3の倍数という性質を使う。  
DPの次元をさらに追加し、桁ごとの数字の和を3で割った余りごとに分けて数える。

```haskell
step3 :: [Int] -> Integer
step3 as = ans
  where
    bnds = ((False,False,0),(True,True,2))     -- A未満/3を含む/桁ごとの和 mod 3
    initial :: Array (Bool,Bool,Int) Integer
    initial = accumArray (+) 0 bnds [((False,False,0),1)]
    final = foldl step initial as
    step arr ai = accumArray (+) 0 bnds
      [ (( less || d < ai
         , has3 || d == 3
         , mod (r + d) 3)                      -- 今回の数字を加えて3で割った余りの位置に足しこむ
        , val)
      | ((less,has3,r),val) <- assocs arr
      , val > 0
      , d <- [0..if less then 9 else ai]
      ]
    ans = sum [val | ((_, b, r),val) <- assocs final, b || r == 0] -- 3がある or mod 3 == 0
```

```
> step3 [4,2,7,5,6,3,1]
3105542
(0.01 secs)
> length [() | i <- [0..4275631], elem '3' (show i) || mod i 3 == 0]
3105542
(4.21 secs)
```

### ステップ4

A以下の非負整数のうち、「3が付くまたは3の倍数」かつ「8の倍数でない」数の総数を求める

8の倍数に関して区別して数えるために、 DPの次元をさらに追加する。

ここで、8の倍数かどうかを判定するのには、
最後の3桁の1000とおりで分類する必要がある？と思ってしまうが、
実は8とおりの分類だけでいけるらしい。  
ある数の最後の1桁が $d$ 、それより上の桁が $X$ というその数は $10X+d$ である。
これを 8 で割った余りは
$$
\begin{array}{ll}
(10X+d) \bmod 8 & = (10 \bmod 8 \cdot X \bmod 8 + d \bmod 8) \bmod 8 \\
& = (2 \, (X \bmod 8) + d) \bmod 8
\end{array}
$$
となるので、 $X$ そのもの（または1000で割った余り）を保持する必要はなく、
8の剰余 $X \bmod 8$ だけで次の桁まで含めた8の剰余が得られる。
（倍数は2でなく10のままでもよいだろう。）

```haskell
step4 :: [Int] -> Integer
step4 as = ans
  where
    bnds = ((False,False,0,0),(True,True,2,7))     -- …/mod 8
    initial :: Array (Bool,Bool,Int,Int) Integer
    initial = accumArray (+) 0 bnds [((False,False,0,0),1)]
    final = foldl step initial as
    step arr ai = accumArray (+) 0 bnds
      [ (( less || d < ai
         , has3 || d == 3
         , mod (r + d) 3
         , mod (2 * x + d) 8) -- 8で割った余り
        , val)
      | ((less,has3,r,x),val) <- assocs arr
      , val > 0
      , d <- [0..ub = if less then 9 else ai]
      ]
    ans = sum [val | ((_, b, r, x),val) <- assocs final, b || r == 0, x /= 0] -- … かつ mod 8 ≠ 0
```

```
> step4 [4,2,7,5,6,3,1]
2738015
(0.04 secs)
> length [() | i <- [0..4275631], elem '3' (show i) || mod i 3 == 0, mod i 8 /= 0]
2738015
(5.31 secs)
```

### 結局

- 条件の数だけDP配列の次元を増やして、
- 次の桁に進むごとに、その桁の数字を続けた数がどの場合に計上されるかを考えてそれに足しこむ
- 最後に、条件にあう分割について足し合わせる

としている。
珍しく「配る」DPになっているのは、
配る範囲と配られる範囲が明確に定まるから実現できている感じ。


## その他

「B以上A以下」という条件のときは、「A以下」の個数－「B未満」の個数、で求めるか、
ステップ1での「A以下」と同様に「B以上」という条件を加えて求めるか、
どちらも計算量は同じで、前者の方がコードは単純で済む感じ。

場合の数が膨大になる問題では、$\bmod (10^9+7)$ で答える場合がある。
このとき、`step` 関数の `accumArray` の引数には剰余付き加算を指定しても、
`ans` で答えを寄せる場所の `sum` を忘れる場合があるので[注意](https://atcoder.jp/contests/abc129/submissions/29854113)

```haskell
  where
     :
    step arr li = accumArray add 0 bnds ...
     :
    ans = foldl add 0 [...

modBase = 10^9 + 7
add x y = mod (x + y) modBase
```

## 関連問題

- [ABC154 E Almost Everywhere Zero](https://atcoder.jp/contests/abc154/tasks/abc154_e) - [ACコード](https://atcoder.jp/contests/abc154/submissions/29845180)
- [ABC135 D Digits Parade](https://atcoder.jp/contests/abc135/tasks/abc135_d) - [ACコード](https://atcoder.jp/contests/abc135/submissions/29845638)
- [ABC029 D 1](https://atcoder.jp/contests/abc029/tasks/abc029_d) - [ACコード](https://atcoder.jp/contests/abc029/submissions/29847040)
- [ABC101 D Snuke Numbers](https://atcoder.jp/contests/abc101/tasks/arc099_b)

他サイト

- [yukicoder189 SUPER HAPPY DAY](https://yukicoder.me/problems/no/189)
- [yukicoder1417 100の倍数かつ正整数(2)](https://yukicoder.me/problems/no/1417)

十進法以外の桁DP

- [ABC129 E Sum Equals Xor](https://atcoder.jp/contests/abc129/tasks/abc129_e) - [ACコード](https://atcoder.jp/contests/abc129/submissions/29859485)
- [ARC129 A Smaller XOR](https://atcoder.jp/contests/arc129/tasks/arc129_a) - [ACコード](https://atcoder.jp/contests/arc129/submissions/29849751)

他サイト

- [yukicoder1740 Alone 'a'](https://yukicoder.me/problems/no/1740)
