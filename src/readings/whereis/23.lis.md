---
order: -23000
description: "Longest Increasing Subsequence, LIS"
---

# 23.最長増加部分列

[最長増加部分列](https://tjkendev.github.io/procon-library/python/dp/lis.html)

長さ $N$ の数列 $[a_1, a_2, \dots, a_N]$ に対して、
$a_{i_1} < a_{i_2} < \dots < a_{i_k}$ となる $i_1 < i_2 < \dots < i_k$ の中で
最大の $k$ や、添え字列 $[i_j]$ や部分列 $[a_{i_j}]$ を求める

つまり、飛び飛びにより大きい要素を拾って作れる最も長い部分列を作れということ。

## 動的プログラミング

数列の前からk項を使って作れる増加部分列の情報を保持し、kを1ずつ増やす、
つまり数列の要素を先頭から順に調べることで、最終的に数列全体を使って作れる増加部分列の情報を導く。

**DPの情報**  
$k$ 項までの数列からは、長さ $1$ から最大 $k$ まで様々な長さの増加部分列が作れる。
また、同じ長さの異なる増加部分列が同時に存在しうる。
ここで、その長さ $i$ ごとに、その長さの増加部分列で末尾が最も小さいものの末尾の値を保持する。
これを $\textit{last}_k[i]$ とする。

**ステップ動作(1)**  
さて、次の項 $a_{k+1}$ を見たとき、$\textit{last}_k[i] < a_{k+1}$ ならば、
長さ $i$ の増加部分列に $a_{k+1}$ を続けることで長さ $i+1$ の増加部分列が作れるとわかる。
さらにここで $a_{k+1} < \textit{last}_k[i+1]$ ならば、
より小さな末尾を持つ長さ $i+1$ の増加部分列が見つけられた。
末尾が小さいほどさらに延長しやすいので、より小さな末尾の値を記録する。
更新されないときは元の値のままにして、 $\textit{last}_{k+1}[1 \sim k+1]$ が定義される。

**DPの初期値**  
記憶する情報の初期値は $\textit{last}_1[1] = a_1$, $i > 1$ について $\textit{last}_1[i] = \infty$ とする。

**DP情報の性質とステップ動作の詳細**  
更新が起きるのは $\textit{last}_k[i] < a_{k+1} < \textit{last}_k[i+1]$ のときで、  
このとき $\textit{last}_{k+1}[i] = \textit{last}_k[i] < \textit{last}_{k+1}[i+1] = a_{k+1}$ となる。  
$\textit{last}_1[*]$ は単調増加であり、上のステップ操作は列が単調増加であるときその性質を壊さないので、
全ての $\textit{last}_k[*]$ は単調増加である。
また、単調増加な列に対して、上のステップ操作は、たかだか一か所でしか起きない。
操作するべき位置 $i$ は $a_{k+1}$ を挟む位置で、二分探索で特定できる。

以上の考察をコードに直す。  
二分探索を配列相手にするなら、`Data.IntSet.lookup*`を使う方が直接的なのでそうしてみる。

長さ1の列について更新するために、仮想的に $\textit{last}[0] = \infty$ とする。  
ただし、上昇列を復元する必要がなければ、 $a_{k+1}$ より小さい要素を探すことそのものが不要となる。

整数要素に対して、列の長さだけを求める版

```haskell
import qualified Data.IntSet as IS
import Data.Maybe

-- @gotoki_no_joe
lengthLIS :: [Int] -> Int
lengthLIS [] = 0
lengthLIS (x:xs) = IS.size final
  where
    initial = IS.singleton x
    final = foldl' {-'-} step initial xs
    step lastk ak1
      | IS.member ak1 lastk = lastk
      | isNothing u = IS.insert ak1 lastk
      | otherwise   = IS.insert ak1 $ IS.delete (fromJust u) lastk
      where
        u = IS.lookupGT ak1 lastk
```

順序のある任意の要素に対して、列を復元する版  
列の末尾をキーに、列の要素リスト（逆順）をラベル付けするMapへ変更する。  
また、要素型に `Bounded` を義務付ける代わりに、
maxBoundとして使える十分に大きな値を貰うことにする。

```haskell
import qualified Data.Map as M
import Data.Maybe

-- @gotoki_no_joe
longuestIncreasingSubsequence :: Ord a => a -> [a] -> [a]
longuestIncreasingSubsequence tooBig xs = reverse $ snd $ findMax final
  where
    initial = M.singleton tooBig []
    final = foldl' {-'-} step initial xs
    step lastk ak1
      | IS.member ak1 lastk = lastk
      | isNothing u = IS.insert ak1 lastk
      | otherwise   = IS.insert ak1 (ak1 : ls) $ IS.delete (fst $ fromJust u) lastk
      where
        Just (l,ls) = IS.lookupLT ak1 lastk
        u = IS.lookupGT ak1 lastk
```

### Mutable Vector版

DP配列をIntSetで実装したコードは美しいが、シビアな状況では時間がかかりすぎる。
mutable vectorを使うことで性能を求めると、
これを二分探索するコードがこのvectorをアクセスする操作もモナド計算になってしまう。

競プロ典型 90 問 60 Chimera のために作成したコード。  
対象の列に添え字でアクセスするための（こちらはpureな）関数を引数にとり、
全ての位置でのその時点でのLISの長さを持つVectorを返す。

```haskell
lengthLISs :: Int -> (Int->Int) -> V.Vector Int
lengthLISs n vf = V.create action
  where
    action :: ST s (V.MVector s Int)
    action = do
      v <- MV.new n
      m <- MV.replicate n maxBound
      foldM_ (step v m) 0 [0..pred n]
      return v
    step v m len i = do
      let ak1 = vf i
      (a,b) <- binarySearchM (\i -> MV.read m i >>= return . (ak1 <)) (-1) len
      through <-
        if a < 0 then return False else do
          ma <- MV.read m a
          return $ ma == ak1
      if through then do
        MV.write v i len
        return len
      else do
        let len1 = if b == len then succ len else len
        MV.write v i len1
        MV.write m b ak1
        return len1

binarySearchM :: Monad m => (Int -> m Bool) -> Int -> Int -> m (Int, Int)
binarySearchM prop unsat sat = loop unsat sat
  where
    loop ng ok
      | abs (ok - ng) <= 1 = return (ng, ok)
      | otherwise = do
          let mid = div (ok + ng) 2
          res <- prop mid
          if res then loop ng mid else loop mid ok
```

これをベースに、全体のLISの長さだけを求める普通の版を切り出しておく。

```haskell
lengthLIS :: Int -> (Int->Int) -> (Int, Int) -- 長さと末尾の値
lengthLIS n vf = runST action
  where
    action :: ST s (Int, Int)
    action = do
      m <- MV.replicate n maxBound
      len <- foldM (step m) 0 [0..pred n] -- ここで止めれば長さだけ求める
      val <- MV.read m len
      return (len, val)
    step m len i = do
      let ak1 = vf i
      (a,b) <- binarySearchM (\i -> MV.read m i >>= return . (ak1 <)) (-1) len
      through <-
        if a < 0 then return False else do
          ma <- MV.read m a
          return $ ma == ak1
      if through then return len else do
        MV.write m b ak1
        return $ if b == len then succ len else len

binarySearchM :: Monad m => (Int -> m Bool) -> Int -> Int -> m (Int, Int)
binarySearchM prop unsat sat = loop unsat sat
  where
    loop ng ok
      | abs (ok - ng) <= 1 = return (ng, ok)
      | otherwise = do
          let mid = div (ok + ng) 2
          res <- prop mid
          if res then loop ng mid else loop mid ok
```

## 足踏みを許す

上昇に足踏みを許す、すなわち等号を含む
$a_{i_1} \leq a_{i_2} \leq \dots \leq a_{i_k}$ となる最長の部分列を探す場合は、
$\textit{last}$ の要素が重複しうるため、上のようにSet,Mapでは管理できない。
命令型のコードのように長さ添え字の配列で扱うか、多重集合を表すデータ構造を持ち出す必要がある。

## 関連問題

- [ABC006 D トランプ挿入ソート](https://atcoder.jp/contests/abc006/tasks/abc006_4) - [ACコード](https://atcoder.jp/contests/abc006/submissions/29957800)
- [ABC134 E Sequence Decomposing](https://atcoder.jp/contests/abc134/tasks/abc134_e) - [ACコード](https://atcoder.jp/contests/abc134/submissions/29964374) LISそのものでなく発想としての関連問題
- (expert!) [AGC024 B Backfront](https://atcoder.jp/contests/agc024/tasks/agc024_b) - [ACコード](https://atcoder.jp/contests/agc024/submissions/29964606) こちらも同じく発想としての関連問題
