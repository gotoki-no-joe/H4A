---
order: -05000
---

# 5.エラトステネスの篩

## お話

蛇足から始める。

配列を用いた命令型の篩は、素数を見つけるたびにその整数倍を全て除去する。
このとき使う計算は掛け算で、それはそれほど遅くない。

一方、Haskellの教科書の前の方に書いてある典型的な篩は、
素数を見つけるたびに、その素数で割り切れる値を除去するフィルタを追加する。

```Haskell
primes0 :: [Int]
primes0 = 2 : sieve [3,5..]
  where
    sieve (x:xs) = x : sieve (filter (nd x) xs)
    nd x y = mod y x /= 0 -- "Not Divisible"
```

これは、除算 `mod` を使うため効率が悪く、
また見つけた素数の数だけ `filter` が入れ子になるのでどんどん重くなる、
いわゆる「やってはいけないHaskell」の一つである。

```haskell
primes0 = 2 : sieve [3,5..] =
2 : 3 : sieve (filter (nd 3) [5,7..]) =
2 : 3 : 5 : sieve (filter (nd 5) (filter (nd 3) [7,9..])) =
2 : 3 : 5 : 7 : sieve (filter (nd 7) (filter (nd 5) (filter (nd 3) [9,11..]))) =
2 : 3 : 5 : 7 : 11 : sieve (filter (nd 11) sieve (filter (nd 7) (filter (nd 5) (filter (nd 3) [13,15..])))) =
...
```

篩を `filter` の連鎖で実現する代わりに、
発見済みの素数の倍数の昇順のリストで表現することを考える。
そのような、無限リストを作りマージする方法は、
手続き的な篩を再現していると言える。

```haskell
primes1 :: [Int]
primes1 = 2 : sieve [] [3,5..]
  where
    sieve (x:xs) (p:ps) =
      case compare x p of
        LT -> sieve xs (p:ps)
        EQ -> sieve xs ps
        GT -> p : sieve (merge (x:xs) [p*2,p*3..]) ps
    merge xs [] = xs
    merge (x:xs) (y:ys) =
      case compare x y of
        LT -> x : merge xs (y:ys)
        EQ -> x : merge xs ys
        GT -> y : merge (x:xs) ys
```

しかしこれも、 `[p*2,p*3..]` の実体である `enumFromThen (p*2) (p*3)` と `merge` が
発見済みの素数の数だけ並ぶ。
この合成数リストと同じ結果をよりコンパクトに達成するために、
素数 p ごとに、次に出現するその倍数 np だけを優先度付きキューに入れて、小さいものから取り出す。
取り出した np を消費したら (n+1)p をキューに投入する。
そのためには元の p も紐づけておく必要がある。

```haskell
primes2 :: [Int]
primes2 = 2 : sieve [] [3,5..]
  where
    sieve ((np,p):nps) (x:xs) | np <  x =     sieve (insert (np+p,p) nps) (x:xs)
                              | np == x =     sieve (insert (np+p,p) nps)    xs
    sieve         nps  (x:xs)           = x : sieve (insert (x +x,x) nps)    xs
```

新たに見つかった素数 $x$ を合成数リストに登録するとき、$x$ より小さい素数 $p$ について $px$ は全て
$p$ の倍数として除去済みと言える。なので初期値は $2x$ でなく $x^2$ でよい。
また、それ以降の増加幅も（奇数だけを気にしているので）$2x$ ずつでよい。
さらに優先度付きキューを `Data.List` による近似から `Data.Heap` にし、
キューが空の場合をなくしたものを
[素数（エラトステネスの篩）](/snippets/integer/primes/)に示す。


## 関連問題

- [天下一プログラマーコンテスト2012 予選C A](https://atcoder.jp/contests/tenka1-2012-qualC/tasks/tenka1_2012_9) - [ACコード](https://atcoder.jp/contests/tenka1-2012-qualC/submissions/27486067)
- [ABC149 C Next Prime](https://atcoder.jp/contests/abc149/tasks/abc149_c) - [ACコード](https://atcoder.jp/contests/abc149/submissions/27486099)
- [ABC170 D Not Divisible](https://atcoder.jp/contests/abc170/tasks/abc170_d) - このコードを使う形では解けていない。別のページで解いているようだ。

## 性能測定

これらの性能を計測すると次のような結果が得られた。(GHCiで実行)
filter modとmergeは2,000個、他は10,000個の素数を求めている。

|方式|時間 (μ秒)|
|----|----:|
|filter mod (2000個)|2,343,750|
|merge (2000個)|5,750,000|
|List.insert|171,875|
|Heap|312,500|
|Vector|156,250|

- 2021/12/28 除去候補の初期値は3xでなくx^2でよいことに気づいた。ら、List版がHeap版を追い抜いた。
- 2022/1/28 modを使わない篩を追加。modを使う版より遅い。
