---
order: -07000
---
# 7.Union-Find

集合の要素を、交わりのない部分に分割する。
全ての要素が、自分だけが属する分割にいる状態から始める。
「以後、要素aとbは同じ分割に属する」という分割の統合(union)と、
「現在、要素aとbは同じ分割に属しているか？」という問い合わせ(find)を
効率的に行えるデータ構造およびアルゴリズム。

グラフの最小全域木を作るクラスカル法、二値画像の連結成分の抽出など、
他のアルゴリズムのバックエンドとしても活躍する。

参考
- [Wikipedia 素集合データ構造](https://ja.wikipedia.org/wiki/%E7%B4%A0%E9%9B%86%E5%90%88%E3%83%87%E3%83%BC%E3%82%BF%E6%A7%8B%E9%80%A0)
- [PythonでのUnion-Find（素集合データ構造）の実装と使い方](https://note.nkmk.me/python-union-find/)

集合の要素を添え字とする配列を用意し、
内容には、自分と同じ分割に属する（成り行き上の）代表元に至る木をなすような、親へのリンクを保持する。

命令型配列の、 $O(1)$ の高速なランダムアクセス、特にリンクの書き換えがアルゴリズムの要であるため、
`Data.Array.(//)` や `Data.Vector.(//)` での模倣は性能が得られないが、
はじめは説明のために Array で作ってみよう。

## 素朴な実装

集合の要素を上限n-1までの0以上の整数と設定する。
配列の添え字は0からn-1まで、内容は同じ分割に属する他の要素の番号、
ただし自分が代表元（木の根）であるとき自分自身とする。
（これは、代表元であるか否かを区別できるなら何でも構わないので、
後で割と自由に使う。）

```haskell
import Data.Array

type UnionFind = Array Int Int
```

配列の初期値は、全て自分自身の値を持つことになる。

```haskell
newUF :: Int -> UnionFind
newUF n = listArray (0, pred n) [0..pred n]
```

指定の番号の要素から、その要素が属する分割の代表元の番号を得る操作が普通に欲しいだろう。

```haskell
getRoot :: UnionFind -> Int -> Int
getRoot uf i = let k = uf ! i in if i == k then i else getRoot uf k
```

ふたつの要素を指定してのfindは、リンクを辿ることでそれぞれの代表元を求め、
同じところに到達したならば同じ分割にある。

```haskell
findUF :: UnionFind -> Int -> Int -> Bool
findUF uf a b = getRoot uf a == getRoot uf b
```

ふたつの要素を指定してのuniteは、やはりまずそれぞれの代表元を求め、
（それらが異なるとき）いずれかの親をもう一方に更新して、根に接ぎ木する。

```haskell
uniteUF :: UnionFind -> Int -> Int -> UnionFind
uniteUF uf i j = let a = getRoot uf i
                     b = getRoot uf j
                 in if a == b then uf else uf // [(a,b)]
```

これで一通りの機能は実装できた。

## 木の高さを考慮した効率化

上の素朴な実装では、unite操作の順により、木が一本道になりうる。
このとき `getRoot` に $O(n)$ かかり、期待した効率が得られない。
そうならないように、木を極力低く保つ必要がある。
その対処は二通り知られており、両方を組み合わせることもできるが、片方だけでもそれなりに動作するらしい。

ひとつは経路圧縮という。
`getRoot` の際に通過した要素全てについて、それらを代表元の直接の子になるようにリンクをつなぎ直す。
これはimmutableな `getRoot` に配列の（意味を変更しないが）mutableな変更を副作用として持ち込むもので、
Haskellとは相性が非常によくない。

もうひとつは、unite操作における接ぎ木をする際に木の高さを意識して、
高い方を根にし、低い方の木をその子にすることである。
両方の木の高さを $h_1 \geq h_2 + 1$ とすると、結果の木の高さは $\max(h_1, h_2+1) = h_1$ となる。
$h_1 = h_2$ のとき、（どちらを根にしても）結果の木の高さは $h_1+1$ となる。
uniteは元々UnionFind配列に対する更新操作なので、これは導入するべきである。
この場合木の高さが $O(\log n)$ で抑えられるらしい。

木の高さを参照する必要があるのは、根（代表元）に関してのみである。
分割の全ての要素が自分の木の高さを知っている必要はない。
そこで、配列の要素が「代表元かどうかを判別できれば、代表元の値は何でもよい」ことを利用し、
代表元の場合は木の高さをマイナスにした値を持つことにする。
（Haskell原理主義的には、 `Either (木の高さ) (親の番号)` のようにするべきという意見も一理あるが、
符号ビットを `Either` のタグだとおもえば同じことである。）
すると、それぞれの関数を次のように直すことになる。

- `newUF` では、初期値をそのノード番号ではなく全て-1にする。
- `getRoot` では、代表元に到達したかの判定方法が変わる。
- `uniteUF` では、高い方の木に接ぎ木すること、接ぎ木の結果の作り方が変わることに注意する。

```haskell
newUF :: Int -> UnionFind
newUF n = listArray (0, pred n) $ replicate n (-1)

getRoot :: UnionFind -> Int -> Int
getRoot uf i = let k = uf ! i in if 0 > k then i else getRoot uf k

uniteUF :: UnionFind -> Int -> Int -> UnionFind
uniteUF uf i j
  | a == b = uf
  | True   = case compare r s of
               GT -> uf // [(a,b)]
               LT -> uf // [(b,a)]
               EQ -> uf // [(b,a), (a, pred r))]
  where
    (a, r) = (getRoot uf i, uf ! a)
    (b, s) = (getRoot uf j, uf ! b)
```
 
## mutable vector化と経路圧縮

上の `(//)` を使うコードは、説明としては見通しがよいが、効率の点で劣る。
このため、 `Data.Vector.Mutable` を用いる形に直す。
mutable vectorは読みだすだけでアクションになるため、
同時に `getRoot` で経路圧縮も行う。
そうするとき、均すために木の高さを追跡しても、経路圧縮により実際にはそれより木が低くなっていることも多い。
そこで、木の高さと相関のありそうな値として大きさ、要素数を追跡し、unionで接ぎ木をする際には、大きい方を根にする。

また、データ構造への操作は副作用になるため、 `uniteUF` は値を返す必要がなくなる。
そこで、与えられた2要素が初めから同じ分割に属していたとき `False` 、
異なる分割だったものを統合したとき `True` を返すように修正する。

という完成版を
[Union-Find](/H4A/ad/misc/unionfind/)
に示す。

また、それぞれの分割に対して任意の情報を割り当てられるように拡張した版も作成した。

<!--
-- 分割の要素数
sizeUF :: UnionFind -> Int -> IO Int

sizeUF :: UnionFind -> Int -> IO Int
sizeUF vec i = do
  j <- getRoot vec i
  s <- MUV.read vec j
  return (-s)
-->

### 全ての分割

全ての要素について、同じ分割に属するものを集めたリストを得たい場合は、
getRootをキーとするMapに集めるとよい。

```haskell
import Control.Applicative
import qualified Data.IntMap as IM

allDivisions :: UnionFind -> IO [[Int]]
allDivisions vec =
  IM.elems . IM.fromListWith (++) <$>
    forM [0..pred $ MV.length vec] (\i -> do
      r <- getRoot vec i
      return (r, [i])
      )
```

## 関連問題

- [ABC177 D Friends](https://atcoder.jp/contests/abc177/tasks/abc177_d) - [ACコード](https://atcoder.jp/contests/abc177/submissions/22742331) 旧タイプ
- [ACL Beginner Contest C](https://atcoder.jp/contests/abl/tasks/abl_c) - [ACコード](https://atcoder.jp/contests/abl/submissions/27497827)
- [ARC106 B Values](https://atcoder.jp/contests/arc106/tasks/arc106_b) - [ACコード](https://atcoder.jp/contests/arc106/submissions/28698922) 任意ペイロード版
- [ARC114 B Special Subsets](https://atcoder.jp/contests/arc114/tasks/arc114_b) - 【ACコード】
- [ABC157 D Friend Suggestions](https://atcoder.jp/contests/abc157/tasks/abc157_d) - [ACコード](https://atcoder.jp/contests/abc157/submissions/28673652)
- [ABC120 D Decayed Bridges](https://atcoder.jp/contests/abc120/tasks/abc120_d) - [ACコード](https://atcoder.jp/contests/abc120/submissions/28669145)
- [ARC111 B Reversible Cards](https://atcoder.jp/contests/arc111/tasks/arc111_b) - 【ACコード】
- [ABC183 F Confluence](https://atcoder.jp/contests/abc183/tasks/abc183_f) - [ACコード](https://atcoder.jp/contests/abc183/submissions/28677604) 任意ペイロード版

その他
- [yukicoder No.1390 Get together](https://yukicoder.me/problems/no/1390)
- [MojaCoder Bonsai](https://mojacoder.app/users/magurofly/problems/bonsai)

nkmk.meより
- [AtCoder Typical Contest 001 B - Union Find](https://atcoder.jp/contests/atc001/tasks/unionfind_a)
- [互いに素な集合 Union Find| データ構造ライブラリ | Aizu Online Judge](http://judge.u-aizu.ac.jp/onlinejudge/description.jsp?id=DSL_1_A&lang=jp)


## おまけ : The Algorithms版

要素を整数に限定しない、最も素直な実装が[The Algorithms/Haskell/MergeFindSet](https://github.com/TheAlgorithms/Haskell/blob/master/src/SpecializedStructure/MergeFindSet.hs)にあった。

本書の言葉遣いに直した版を示す。

```haskell
import qualified Data.Map as M
import qualified Data.Set as S

type UnionFind a = (M.Map a a, M.Map a Int)

newUF :: Ord a => [a] -> UnionFind a
newUF xs = (fathers, ranks)
  where
    fathers = M.fromList [(x, x) | x <- xs]
    ranks   = M.fromList [(x, 0) | x <- xs]

getRoot :: Ord a => a -> UnionFind a -> a
getRoot x uf = let father = (fst uf) M.! x
               in  if father == x then x else getRoot father uf

findUF x y uf :: Ord a => a -> a -> UnionFind a -> Bool
findUF x y uf = getRoot x uf == getRoot y uf

uniteUF :: Ord a => a -> a -> UnionFind a -> UnionFind a
uniteUF x y uf@(fathers, ranks)
  | p == q = uf
  | True = case comapre a b of
      LT -> (p `connectTo` q, ranks)
      GT -> (q `connectTo` p, ranks)
      EQ -> (p `connectTo` q, M.adjust succ q ranks) -- M.insert q (succ b) ranks
  where
    (p, a) = (getRoot x uf, ranks M.! p)
    (q, b) = (getRoot y uf, ranks M.! q)
    connectTo a b = M.adjust (const b) a fathers -- M.insert a b fathers
```
`adjust` を意図的に使っているけれど、`insert`を使うのと何が違うのかしら...

経路圧縮もしていないし、性能は期待しない方がよさそう。
`connectTo` を関数に抽出しているところは美しいので見習いたい。
