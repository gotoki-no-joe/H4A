---
order: -29000
---

# 29.セグメント木

一次元配列があり、その様々な区間に対して特定の演算を行った結果を何度も求めたり、
配列の要素を更新したりする操作が $O(\log n)$ で行えるデータ構造。  
例えば、配列の要素を更新しながら、さまざまな区間に対して区間和を求めたりできる。

- [ACL for Pythonの説明](https://github.com/shakayami/ACL-for-python/wiki/segtree)
- [原理の説明](https://algo-logic.info/segment-tree/#:~:text=%E3%82%BB%E3%82%B0%E3%83%A1%E3%83%B3%E3%83%88%E6%9C%A8%E3%81%A8%E3%81%AF%E3%80%81%E5%AE%8C%E5%85%A8,%E9%A0%BB%E5%87%BA%E3%81%A8%E3%81%AA%E3%81%A3%E3%81%A6%E3%81%84%E3%81%BE%E3%81%99%E3%80%82)  

## 解説

正体は完全二分木とモノイド演算（結合律を満たし単位元を持つ二項演算）の対である。配列の元データは葉に記録する。
要素数が $2^n$ 個に満たないとき、余った要素は演算の単位元で埋めておく。
木の中間ノードはその子孫の全ての葉に対して演算を行った結果を持つ。

データ構造には、木の本体の他に、木の幅と、モノイド演算も記録しておくこととする。

```haskell
data SegmentTree a = SegmentTree Int       -- 木の幅
                                 (a->a->a) -- 演算
                                 a         -- 単位元
                                 (STree a) -- 二分木

data STree a = Leaf a | Node a (STree a) (STree a) -- private
```

木の末端から順に、中間ノードの保持する値を求めることで木を構築する。  
（左側の子を作るときにリストを `lt = loop w2 (take w2 xs)` と切り取らずに渡しているのは、
`drop`はリンクリストを辿るだけなのに対して、
`take`は新しいセルの割り当てとコピーを伴うので遅いため、
その必要がないところで`take`しない小技である。）

```haskell
makeSegTree :: [a] -> (a->a->a) -> a -> SegmentTree a
makeSegTree xs op u = SegmentTree w op u t
  where
    len = length xs
    w = head $ dropWhile (len >) $ iterate (2 *) 1
    t = loop w (xs ++ repeat u)
    loop 1 (x:_) = Leaf x
    loop w xs = mkNode op lt rt
      where
        w2 = div w 2
        lt = loop w2 xs
        rt = loop w2 $ drop w2 xs

-- utilities
getVal (Leaf x) = x
getVal (Node x _ _) = x

mkNode op lt rt = Node (getVal lt `op` getVal rt) lt rt
```

要素を更新した際、その親ノードの値も影響を受けるので更新する。  
添え字は0からw-1とする。  
自分の管理する要素数の半分を超える位置の要素なら右、さもなくば左に更新を伝達し、結果を受けて自分も更新する。

```haskell
updateSegTree :: SegmentTree a -> Int -> a -> SegmentTree a
updateSegTree (SegmentTree w op u t) j x = SegmentTree w op u t1
  where
    t1 = loop w j x t
    loop 1 _ x _ = Leaf x -- arg2 = 0, arg4 = Leaf oldval
    loop w j x (Node _ lt rt)
      | j < w2 = mkNode op lt1 rt
      | True   = mkNode op lt rt1
      where
        w2 = div w 2
        lt1 = loop w2 j x lt
        rt1 = loop w2 (j-w2) x rt
```

区間に対する計算結果は、その区間を覆うノードの値を集めることで求められる。
区間はその要素を含む下限と、含まない上限の添え字で $[a,b)$ と指定する。
現在のノードが受け持つ範囲を復元しながら、再帰的な呼び出しにより求められる値を次のように構成する。
$[a,b)$ の側は固定し、加工しない。

- 根ノードの受け持ち範囲は $[0,w)$ である。その幅は $w$ である。
- 以降、$w$ を半分にしながら、左と右の受け持ち範囲は半々になっていく。  
現在注目している頂点の受け持ち範囲を $[p,q)$ とする。
- 要求範囲が受け持ち範囲を全く外れている場合、単位元を返す。  
- さもなくば、部分的に重なりがあるので、左右の子両方に問い合わせ、結果を演算で結合する。

```haskell
querySegTree :: SegmentTree a -> Int -> Int -> a
querySegTree (SegmentTree w op u t) a b = loop 0 w w t
  where
    loop p q _ t
      | b <= p || q <= a = u
      | a <= p && q <= b = getVal t
    loop l r w (Node _ lt rt)
      | otherwise = loop p m w2 lt `op` loop m q w2 rt
      where
        w2 = div w 2
        m = p + w2
```

<!--
区間に対する更新も、この関数の振る舞いを使えばできそうだ。遅延評価はHaskellが勝手にやってくれる。
この木に対する二分探索をどう抽象化してよいかよくわからない。のでこれも保留。
[完成した実装まとめ](/ad/query/segment-tree)
-->

## Mutable Vectorによる実装

このアルゴリズムの本質は上で述べたような二分木の操作であるが、immutableなTreeでそのまま実現すると、ノードを辿ったり作ったりする計算コストの係数が大きい。

ヒープソートにおいて二分木を一次元配列に押し込めたのと同じ方法で、セグメント木の各ノードを一次元配列に納める。すると木を辿ることは添え字の計算で、ノードの更新はmutable vectorの更新で行え、各操作が効率的に実装できる。

根は添え字0に、添え字 $k$ のノードの二つの子は添え字 $2k+1, 2k+2$ になる。
要素数nをちょうど2のべき乗として、葉をn持つ二分木のノードの総数は
$n + n/2 + n/4 + \dots + 1 = 2n-1$ である。
つまり、添え字が $n-2$ までが中間ノードで、$n-1$ ～ $2n-2$ までの $n$ 要素が葉である。

データ構造は木の定義だけ変更する。

```haskell
import Control.Monad
import qualified Data.Vector.Unboxed.Mutable as MUV

data SegmentTree a = SegmentTree Int (a->a->a) a (STree a)

type STree a = MUV.IOVector a
```

構築は、与えられた初期値を葉の位置に書き込んだあと、
葉のすぐ下のノードから根の方へループで更新することで初期化できる。

初期値と大きさをリストで与える代わりに、
大きさだけを指定して単位元で埋める版や、指定した初期値で埋める版、
などの異なる初期構築の方法が考えられるが、どれがよいのだろう。

```haskell
makeSegTree :: MUV.Unbox a => [a] -> (a->a->a) -> a -> IO (SegmentTree a)
makeSegTree xs op u = do
  let len = length xs
  let w = head $ dropWhile (len >) $ iterate (2 *) 1
  vec <- MUV.new (w * 2 - 1)
  forM_ (zip [0..pred w] (xs ++ repeat u))
        (\(i,x) -> MUV.write vec (i + w - 1) x)
  forM_ [w-2,w-1..0] (\k -> do
    l <- MUV.read vec (k*2+1)
    r <- MUV.read vec (k*2+2)
    MUV.write vec k (op l r)
    )
  return $ SegmentTree w op e vec
```

更新操作では、指定された一点を更新した後、親ノードをたどる添え字計算を、別のループを回して求めている形になった。

```haskell
updateSegTree :: MUV.Unbox a => SegmentTree a -> Int -> a -> IO ()
updateSegTree (SegmentTree w op _ vec) j x =
  do
    MUV.write vec i0 x
    forM_ upwards (\i -> do
      l <- MUV.read vec (i*2+1)
      r <- MUV.read vec (i*2+2)
      MUV.write vec i (op l r)
      )
  where
    i0 = j + w - 1
    upwards = tail $ takeWhile (0 <=) $ iterate step i0
    step i = div (pred i) 2
```

問い合わせ操作も特に難しいことはない。

```haskell
querySegTree :: MUV.Unbox a => SegmentTree a -> Int -> Int -> IO a -- [左,右)で範囲を指定
querySegTree (SegmentTree w op u vec) a b = loop vec 0 w w 0
  where
    loop vec p q w i
      | q <= a || b <= p = return u
      | a <= p && q <= b = MUV.read vec i
      | otherwise = do
        let w2 = div w 2
        let m = p + w2
        l <- loop vec p m w2 (i*2+1)
        r <- loop vec m q w2 (i*2+2)
        return (op l r)
```

querySegTreeのloopへ不自然にvecを渡しているのは、型のため。

両方の版を[セグメント木](/ad/query/segment-tree)に置く。

## 使えるモノイド演算の例

丸パクリどころか一部は何かもよくわからないまま転載。

| 　 | 演算 | 単位元 |
| :--- | :--- | :--- |
| 和 | \(+\) | 0 |
| 積 | \(\*\) | 1 |
| 最小値 | min | maxBound |
| 最大値 | max | minBound |
| 最大公約数 | gcd | 0 \(\*\) |
| 最小公倍数 | lcm | 1 |
| 排他的論理和 \(\*\) | xor | zeroBits |
| ビットごとの論理積 | \(.&.\) | complement zeroBits |
| ビットごとの論理和 | \(.\|.\) | zeroBits |

0は任意の数で「割り切れる」ため、0と任意の数xの最大公約数はxとなり、0はgcdの単位元といえる、という理屈のようだ。

ビット演算は Data.Bits モジュールにある。

他に、多項式の積、単位元は定数式1、`\(a,b) (c,d) -> (a*c, a*d+b)` 単位元は\(1,0\)、行列積で単位元は単位行列、というものもあるらしい。

## 関連問題

- [ABC185 F Range Xor Query](https://atcoder.jp/contests/abc185/tasks/abc185_f) - [ACコード](https://atcoder.jp/contests/abc185/submissions/23133946)　セグメント木の例題 immutable版
- [ABC123 C GCD on Blackboard](https://atcoder.jp/contests/abc125/tasks/abc125_c) - [ACコード](https://atcoder.jp/contests/abc125/submissions/28989631) immutable版
- [ACL Beginner Contest D Flat Subsequence](https://atcoder.jp/contests/abl/tasks/abl_d) - [ACコード](https://atcoder.jp/contests/abl/submissions/28994033) mutable版が必須

他サイト

- [yukicoder No.875 Range Mindex Query](https://yukicoder.me/problems/no/875)

ABC185Fについて、XORの性質から、書き換えのときに値を下から持ち上げる必要すらない。
足し算に対して引き算のような、逆演算がある場合、その性質を活かした修正操作を準備するべきだろうか。

ACL Dについて、immutable treeな実装では[TLE](https://atcoder.jp/contests/abl/submissions/28990296)だった。
