---
order: -29000
---

# 29.セグメント木

一次元配列があり、その様々な区間に対して特定の演算を行った結果を何度も求めたり、
配列の要素を更新したりする操作が $O(\log n)$ で行えるデータ構造。  
例えば、配列の要素を更新しながら、さまざまな区間に対して区間和を求めたりできる。

- [ACL for Pythonの説明](https://github.com/shakayami/ACL-for-python/wiki/segtree)
- [原理の説明](https://algo-logic.info/segment-tree/#:~:text=%E3%82%BB%E3%82%B0%E3%83%A1%E3%83%B3%E3%83%88%E6%9C%A8%E3%81%A8%E3%81%AF%E3%80%81%E5%AE%8C%E5%85%A8,%E9%A0%BB%E5%87%BA%E3%81%A8%E3%81%AA%E3%81%A3%E3%81%A6%E3%81%84%E3%81%BE%E3%81%99%E3%80%82)  

原理の方では、要素一つの更新でなく区間に対する更新を効率的に行う「遅延評価セグメント木」も説明されているが、ACL for Pythonにはなさそうなのと、理解が追いつかないので今後の課題とする。

## 解説

正体は完全二分木とモノイド演算（結合律を満たし単位元を持つ二項演算）の対である。配列の元データは葉に記録する。
要素数が $2^n$ 個に満たないとき、余った要素は演算の単位元で埋めておく。
木の中間ノードはその子孫の全ての葉に対して演算を行った結果を持つ。

データ構造には、木の本体の他に、木の幅と、モノイド演算も記録しておくこととする。

```haskell
data SegmentTree a = SegmentTree Int       -- 木の幅
                                 (a->a->a) -- 演算
                                 a         -- 単位元
                                 (STree a) -- 二分木

data STree a = Leaf a | Node a (STree a) (STree a) -- private
```

木の末端から順に、中間ノードの保持する値を求めることで木を構築する。

```haskell
makeSegTree :: [a] -> (a->a->a) -> a -> SegmentTree a
makeSegTree xs op e = SegmentTree w op e t
  where
    len = length xs
    w = head $ dropWhile (len >) $ iterate (2 *) 1
    t = loop w (xs ++ repeat e)
    loop 1 (x:_) = Leaf x
    loop w xs = mkNode op lt rt
      where
        w2 = div w 2
        lt = loop w2 xs
        rt = loop w2 $ drop w2 xs

-- utilities
getVal (Leaf x) = x
getVal (Node x _ _) = x

mkNode op lt rt = Node (getVal lt `op` getVal rt) lt rt
```

要素を更新した際、その親ノードの値も影響を受けるので更新する。  
添え字は0からw-1とする。  
自分の管理する要素数の半分を超える位置の要素なら右、さもなくば左に更新を伝達し、結果を受けて自分も更新する。

```haskell
updateSegTree :: SegmentTree a -> Int -> a -> SegmentTree a
updateSegTree (SegmentTree w op i t) j x = SegmentTree w op i t1
  where
    t1 = loop w j x t
    loop 1 _ x _ = Leaf x -- arg2 = 0, arg4 = Leaf oldval
    loop w j x (Node _ lt rt)
      | j < w2 = mkNode op lt1 rt
      | True   = mkNode op lt rt1
      where
        w2 = div w 2
        lt1 = loop w2 j x lt
        rt1 = loop w2 (j-w2) x rt
```

区間に対する計算結果は、その区間を覆うノードの値を集めることで求められる。  
区間は下限と要素数で指定する。  
あるノードに対する問い合わせに対して、次のように振る舞うことになる。

* 下限が0、要素数が自分の幅と等しければ、自身の値が結果である。
* 上限が半分を超えなければ、左の子に丸投げする。
* 下限が半分を超えるならば、右の子に丸投げする。このとき位置をずらす。
* さもなくば両方にまたがっているので、自身の半分の位置で問い合わせを左の子用と右の子用のふたつに分割して、両方に問い合わせて、結果を結合する。

```haskell
querySegTree :: SegmentTree a -> Int -> Int -> a
querySegTree (SegmentTree w op i t) p q
  | q == 0 = i
  | True   = loop w t p q
  where
    loop w t 0 q | w == q = getVal t
    loop w (Node _ lt rt) p q
      | p+q <= w2 = loop w2 lt p q
      | w2  <= p  = loop w2 rt (p-w2) q
      | True = loop w2 lt p q1 `op` loop w2 rt 0 (q-q1)
      where
        w2 = div w 2
        q1 = w2-p
```

区間に対する更新も、この関数の振る舞いを使えばできそうだ。遅延評価はHaskellが勝手にやってくれる。

この木に対する二分探索をどう抽象化してよいかよくわからない。のでこれも保留。

[完成した実装まとめ](/ad/query/segment-tree)

使えるモノイド演算の例をまとめておく。丸パクリだが。

| 　 | 演算 | 単位元 |
| :--- | :--- | :--- |
| 和 | \(+\) | 0 |
| 積 | \(\*\) | 1 |
| 最小値 | min | maxBound |
| 最大値 | max | minBound |
| 最大公約数 | gcd | 0 \(\*\) |
| 最小公倍数 | lcm | 1 |
| 排他的論理和 \(\*\) | xor | zeroBits |
| ビットごとの論理積 | \(.&.\) | complement zeroBits |
| ビットごとの論理和 | \(.\|.\) | zeroBits |

0は任意の数で「割り切れる」ため、0と任意の数xの最大公約数はxとなり、0はgcdの単位元といえる、という理屈のようだ。ただgcdを0まで拡張すると、0と0のgcdが「全ての素数の積」になってしまうような。

ビット演算は Data.Bits モジュールにある。

他に、多項式の積、単位元は定数式1、`\(a,b) (c,d) -> (a*c, a*d+b)` 単位元は\(1,0\)、行列積で単位元は単位行列、というものもあるらしい。

## 関連問題

- [ABC185 F Range Xor Query](https://atcoder.jp/contests/abc185/tasks/abc185_f) - [ACコード](https://atcoder.jp/contests/abc185/submissions/23133946)　セグメント木の例題
- [ABC123 C GCD on Blackboard](https://atcoder.jp/contests/abc125/tasks/abc125_c) - [ACコード](https://atcoder.jp/contests/abc125/submissions/28989631)
- [ACL Beginner Contest D Flat Subsequence](https://atcoder.jp/contests/abl/tasks/abl_d) - [ACコード](https://atcoder.jp/contests/abl/submissions/28994033)

他サイト

- [yukicoder No.875 Range Mindex Query](https://yukicoder.me/problems/no/875)


ABC185Fについて、XORの性質から、書き換えのときに値を下から持ち上げる必要すらない。
足し算に対して引き算のような、逆演算がある場合、その性質を活かした修正操作を準備するべきだろうか。

ACL Dについて、pure functional な実装では[TLEした](https://atcoder.jp/contests/abl/submissions/28990296)。
mutable vectorで実装しないとダメなんか。→でした。

## Mutable Vectorによる実装

性能を得るために、mutable vectorによる実装を試みる。
ヒープソートにおいて二分木を一次元配列に押し込めたのと同じ方法で、セグメント木の各ノードを一次元配列に納める。
根は添え字0に、添え字 $k$ のノードの二つの子は添え字 $2k+1, 2k+2$ になる。
要素数nをちょうど2のべき乗として、葉をn持つ二分木のノードの総数は
$n + n/2 + n/4 + \dots + 1 = 2n-1$ である。
つまり、添え字が $n-2$ までが中間ノードで、$n-1$ ～ $2n-2$ までの $n$ 要素が葉である。

```haskell
import Control.Monad
import qualified Data.Vector.Unboxed.Mutable as MUV

data SegmentTree a = SegmentTree Int       -- 木の幅
                                 (a->a->a) -- 演算
                                 a         -- 単位元
                                 (STree a) -- 二分木

type STree a = MUV.IOVector a

makeSegTree :: MUV.Unbox a => [a] -> (a->a->a) -> a -> IO (SegmentTree a)
makeSegTree xs op e = do
  let len = length xs
  let w = head $ dropWhile (len >) $ iterate (2 *) 1
  vec <- MUV.new (w * 2 - 1)
  forM_ (zip [0..pred w] (xs ++ repeat e))
        (\(i,x) -> MUV.write vec (i + w - 1) x)
  forM_ [w-2,w-1..0] (\k -> do
    l <- MUV.read vec (k*2+1)
    r <- MUV.read vec (k*2+2)
    MUV.write vec k (op l r)
    )
  return $ SegmentTree w op e vec

updateSegTree :: MUV.Unbox a => SegmentTree a -> Int -> a -> IO ()
updateSegTree (SegmentTree w op _ vec) j x =
  do
    MUV.write vec i0 x
    forM_ upwards (\i -> do
      l <- MUV.read vec (i*2+1)
      r <- MUV.read vec (i*2+2)
      MUV.write vec i (op l r)
      )
  where
    i0 = j + w - 1
    upwards = tail $ takeWhile (0 <=) $ iterate step i0
    step i = div (pred i) 2

querySegTree :: MUV.Unbox a => SegmentTree a -> Int -> Int -> IO a -- [左,右)で範囲を指定
querySegTree (SegmentTree w op u vec) a b = loop vec 0 0 w
  where
    loop vec i l r
      | r <= a || b <= l = return u
      | a <= l && r <= b = MUV.read vec i
      | otherwise = do
        let m = div (l + r) 2 -- 何ビットめを1にするかを持ちまわす方が速いか？
        x <- loop vec (i*2+1) l m
        y <- loop vec (i*2+2) m r
        return (op x y)
```

querySegTreeでは、範囲の再計算を行う必要もなくなった。
外れの範囲でも構わず再帰呼び出しをし、呼ばれた側でチェックする。
ぴったりに範囲が重なっているか確認しなくても、おおわれているならそれでよい、という。
これはすごい。
呼ぶ側でも、範囲を踏み越えていても気にせず呼んでいいというのはすごい。

querySegTreeのloopへ不自然にvecを渡しているのは、型のため。
