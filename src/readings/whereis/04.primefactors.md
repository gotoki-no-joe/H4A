---
order: -04000
---

# 4.素因数分解

参考：[Pythonで素因数分解（試し割り法）](https://note.nkmk.me/python-prime-factorization/)

素数の昇順のリスト `primes` があるとする。
与えられた数 n と素数リストの先頭 p = 2 から始め、
n が p で割り切れるならば p を素因数として返しつつ n を p で割る、
を n が 1 になるまで繰り返すことで素因数分解できる。

```haskell #
primeFactors n = loop n primes
  where
    loop n pps@(p:ps)
      | n == 1    = []
      | r == 0    = p : loop q pps
      | otherwise = loop n ps
      where
        (q,r) = divMod n p
```

素数リスト `primes` は `Data.Numbers.Primes` にあるが、
それがあるなら `primeFactors` もあるのでこのページに用はない。
素数でない数が現れてもすぐ次の数に進んでしまうだけのことなので、
代わりに `[2..]` でもよいし、2以上の偶数を除いて `2 : [3,5..]` でもよいし、
「5以上の素数は全て6の倍数±1」という性質を利用して凝ったことをしてもよく、
どれでも代用できる。

```
pseudoPrimes1 = [2..]
pseudoPrimes2 = 2 : [3,5..]
pseudoPrimes3 = 2 : 3 : [y | x <- [6,12..], y <- [pred x, succ x]]
```

それより、行4 の停止条件に関して、
nが大きな素数であったり、大きな素数を素因数に含む数であった場合、
pがそれに届いて割り切るまで終われないことが問題になる。

話の流れから、 $n$ は $p$ より小さい素因数を持つことはないので、
$p x = n$ であるとき $p \leq \sqrt n \leq x$ である。
つまり、$p$ が $\sqrt n$ を超えたときには、$n$ は素数と判断して終わることができる。

という点を改良した完成版を[素因数分解（試し割り法）](/snippets/integer/primefactors/)に示す。

## 関連問題

- [ABC169 D Div Game](https://atcoder.jp/contests/abc169/tasks/abc169_d) - [ACコード](https://atcoder.jp/contests/abc169/submissions/22775113)
- [ABC177 E Coprime](https://atcoder.jp/contests/abc177/tasks/abc177_e) - [ACコード](https://atcoder.jp/contests/abc177/submissions/22737449)
- [ABC152 E Flatten](https://atcoder.jp/contests/abc152/tasks/abc152_e) - [ACコード](https://atcoder.jp/contests/abc152/submissions/28668915) [逆元](14.modrecip/)も参照  
ちなみに、Integerを使えば何の障害もなく[AC](https://atcoder.jp/contests/abc152/submissions/9830048)できる。

他サイト

- [MojaCoder 楔数（くさびすう）](https://mojacoder.app/users/H20/problems/sphenic-number)

## 蛇足

元々は「2で割れるだけ割る」「3以降の奇数で割れるだけ割る」という
Pythonのサンプルコードにひっぱられて

```haskell
-- @gotoki_no_joe
primeFactors :: Int -> [Int]
primeFactors n = loop1 n
  where
    loop1 n
      | even n = 2 : loop1 (div n 2)
      | otherwise = loop2 n 3
    loop2 n f
      | n < f * f = [n | n /= 1]
      | otherwise =
          case divMod n f of
            (q, 0) -> f : loop2 q f
            _      ->     loop2 n (f+2)
```

こんなコードにしていた。このコードが
「試す数を順に生成する」「その数で割れるだけ割る」
という二つの関心をごちゃまぜにした残念な構造であることに
気づくのにかなり時間がかかってしまった。
