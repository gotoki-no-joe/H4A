---
order: -27000
---

# 27.ダイクストラ法（途中）

グラフにおいて、辺の重みが非負であるとき、
指定した開始点から他の全ての頂点への経路で重みの和が最小のものを探すアルゴリズム。
重みを辺の長さとみなして「最短距離」と呼んだりする。混ざる。

[Wikipedia](https://ja.wikipedia.org/wiki/%E3%83%80%E3%82%A4%E3%82%AF%E3%82%B9%E3%83%88%E3%83%A9%E6%B3%95)にある、
優先度付きキューを用いたアルゴリズムを読み解く。

- 入力：グラフの頂点集合V、開始点s、頂点u,v間の辺の重みlength(u,v)
- 出力：開始点から各頂点vまでの最短距離d(v)、最短距離の経路の、vの前の頂点prev(v)
- 作業変数：Qは、まだ距離が確定していない、処理するべき頂点を持つ優先度付きキュー。  
優先度は、vまでの現状での最短距離

``` #
// 初期化
foreach (v ← V)
  d(v) ← (v = s ならば 0、それ以外は∞)
  prev(v) ← 「無し」
  Q に v を優先度 d(v) で加える

// 本計算
while (Qが空でない)
  Qからキュー先頭の頂点uを取り出す
  foreach (uからの辺があるv ∈ V)
    alt ← d(u) + length(u,v)
    if (d(v) > alt)
      d(v) ← alt
      prev(v) ← u
      Q に v を優先度 alt (=d(v)) で加える
```

d(v)は、頂点に対して、探索済み頂点からもう一歩までで到達できる経路における既知の最短距離を割り当てる、配列的なもの。
更新操作ができる。最終的な結果の中核。  
より小さい値へと行12～13で更新されていく初期値として、行3で∞を与えられている。
これをmaxBoundで処理するとダサいので、dはMapとし、写像に含まれないとき最短距離が不明であることを表すとしよう。

prev(v)は、頂点に対して、既知の最短距離になるsからの経路で、vの一つ前の頂点。  
定義済みの値を参照することはなく行14で上書き更新されるだけなので、これも普通にMapで扱える。

Q(v)は距離を優先度として頂点を格納する優先度付きキューである。
行9で、Qに含まれるvの中でd(v)が最小なものを、d(v)をスキャンせずに取り出すために用いられる。
未完了の頂点vについて、d(v)とQ(v)は常に同じ値を持つ。（行13と15で同じaltで更新されるから）
その中で最小のuが行9で1つずつ選択されては処理完了とされ、以降の更新から除外される。

さて、行15で v を Q に優先度 alt で加えるという操作を、もう少し突き詰めて考える必要がある。
普通の優先度付きキューでは値の重複を許すので、
優先度3,2,1で値5を3度追加すると、5が3度取り出せる。
しかしここのQはオリジナルのアルゴリズムでは「未確定な頂点の集合」であり、
ここで本当に意図している動作は
「キューにvが既に異なる（より大きい優先度で）登録されているかもしれないが、
もし存在するならそれは捨てて、改めて優先度altでvを登録する」である。
普通の優先度付きキューはそんな機能を持たない。困った。

この問題への対処として
「異なる優先度でもvが登録されているかもしれないけれど構わずに、
重複を無視してaltでも登録してしまう」という方法がとられているようだ。

ある頂点uが行15で何度か、異なる優先度でQに登録される。
あるタイミングで、それらの中の最小の距離で行9でキューから（ひとつ）取り出される。
このときの距離がuの真の値で、他の、より大きな距離は全て無視してよい。
というのは、本来Qから取り出された頂点はQに戻ることはなく、この瞬間にその頂点の距離 d(u) が確定するからである。

そこで、Qから頂点uを取り出すとき、その優先度pも同時に得て、
p > d(u) ならば不要な要素として捨てるようにする。

という考察に基づいて、Haskell実装を行う。

重みは0, (+), (>) が必要なのでNum（とOrd）である必要がある。  
頂点はd(v)とpred(v)でMapのキーとして用いるので、EqかつOrdである必要がある。  

グラフそのものは、特定のデータ構造で与える代わりに、
頂点に対して隣接する頂点とその辺の重みの対のリストを返す関数として与えることとする。
この関数が様々なグラフの表現に対応するためのアダプタとして機能する。  
キューは[Data.Heap](/library/data.heap)を用いる。

```haskell
import qualified Data.Heap as H
import qualified Data.Map as M

-- @gotoki_no_joe
dijkstra :: (Eq v, Ord v, Ord w, Num w) -- v 頂点 w 辺の重み
         => (v -> [(v,w)])              -- 隣接頂点とその辺の重み、グラフの情報
         -> v                           -- 開始点
         -> (M.Map v w, M.Map v v)      -- 最短経路の重み、最短経路の前の頂点 (routeに渡すもの)
dijkstra graph start = loop queue0 dist0 prev0
  where
    -- 開始点から点vまでの既知の最短距離 d(v) 初期値は d(start) = 0 その他は∞
    dist0 = M.singleton start 0
    -- 既知の最短経路において、vからstartに一つ戻る頂点 prev(v) 初期値は何もなし
    prev0 = M.empty
    -- 探索済み頂点から到達できる頂点のキュー、既知の最小コストが優先度
    queue0 = H.singleton (H.Entry 0 start)
    loop queue dist prev
    -- 繰り返しはキューが空になったら完了
      | H.null queue = (dist, prev)
    -- 先頭ノードが確定済みならスルーして次へ
      | du < cost    = loop queue1 dist prev
    -- 本題
      | otherwise    = loop queue2 dist1 prev1
      where
        Just (H.Entry cost u, queue1) = H.uncons queue
        du = dist M.! u
        -- uから到達できる全てのvについて
        -- u経由でvに到達する経路のコスト du+len=d1 が
        -- 既知のコスト dist M.! v を下回っているものについて
        -- d(v)をd1に、prev(v)をuに更新し、Qのd1にvを登録する
        vds = [(v,d1) | (v,len) <- graph u, let d1 = du + len
                      , M.notMember v dist || d1 < dist M.! v]
        dist1 = M.union (M.fromList vds) dist
        prev1 = M.union (M.fromList [(v,u) | (v,_) <- vds]) prev
        queue2 = H.union (H.fromList [H.Entry d v | (v,d) <- vds]) queue1
```

prevから最短経路を構築できる。
遅延評価なら触らなければprevはthunkのままで一切計算されないだろう。

```haskell
-- prevとstartとvをとり、startからvの系列をリストにする
route prev start v = loop [] v
  where
    loop acc u
      | u == start = u:acc
      | otherwise = loop (u:acc) (prev M.! u)
```

`Data.Map`版は汎用性が高いが、性能は劣る。
ABC160Dを[Map版はTLEした](https://atcoder.jp/contests/abc160/submissions/28964389)。
AtCoder特化で、ノードの表現をIntに限定することで、
`Data.IntMap`に置き換えた[IntMap版はACした](https://atcoder.jp/contests/abc160/submissions/28964412)。
重みの表現に制約を追加しないままで`Data.Vector.Mutable`版を作ることもできるが、
`Unbox`な（ようは数値）の重みなら対応可能な`Data.Vector.Unboxed.Mutable`版は
[高速にACした](https://atcoder.jp/contests/abc160/submissions/28964436)。
これを[完成版](/ad/graph/dijkstra)とする。

## 関連問題

- [ARC109 A Hands](https://atcoder.jp/contests/arc109/tasks/arc109_a) - 【ACコード】
- [ABC160 D Line++](https://atcoder.jp/contests/abc160/tasks/abc160_d) - [ACコード](https://atcoder.jp/contests/abc160/submissions/28964436) 全ての頂点についてダイクストラ法をやりなおすアプローチでACしたが、本来Warshall-Floyd法を使うべきに見える。しかし現状それだとTLE(todo)
- [ABC191 E Come Back Quickly](https://atcoder.jp/contests/abc191/tasks/abc191_e) - 【ACコード】
- [(expert!) ABC192 E Train](https://atcoder.jp/contests/abc192/tasks/abc192_e) - 【ACコード】
- [(expert!※)ZONeエナジー プログラミングコンテスト E 潜入](https://atcoder.jp/contests/zone2021/tasks/zone2021_e) - 【ACコード経路復元あり】【ACコード経路復元なし】

他サイト

- [(expert!) MojaCoder As Soon As Possible](https://mojacoder.app/users/magurofly/problems/as-soon-as-possible)
- [(expert!※) yukicoder No.807 umg tours](https://yukicoder.me/problems/no/807)

