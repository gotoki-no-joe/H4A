---
order: -33000
description: Longest Common Subsequence
---

# 33.最長共通部分列

[Wikipedia 最長共通部分列問題](https://ja.wikipedia.org/wiki/%E6%9C%80%E9%95%B7%E5%85%B1%E9%80%9A%E9%83%A8%E5%88%86%E5%88%97%E5%95%8F%E9%A1%8C)

２つの（文字）列が与えられたとき、同じ長さでそれぞれの部分列を取り出して
等しい文字列を作ったときの最大の長さ、あるいはそのような部分列を求める問題。

## 不自然な導入

素朴なアルゴリズムから話を始める。簡単のため列の長さのみを求める。  
２つの文字列`as`, `bs`の前からそれぞれ i 文字、j 文字までの範囲の最長共有部分列の長さを求める。

```haskell
lcs as bs = recur (length as, length bs)
  where
```

いずれかが長さ0なら結果も0である。

```haskell
    recur (0, _) = 0
    recur (_, 0) = 0
```

i文字めとj文字めが異なるとき、少なくともいずれかは結果に寄与していない。
もう一方も寄与していないかもしれないが、相手のさらに前方にある文字と対応して寄与している可能性もある。
つまり、i-1,j に対する長さと、i,j-1に対する長さのうち大きい方が、i,jに対する長さである。

```haskell
    recur (i, j)
      | as !! pred i /= bs !! pred j = recur (pred i, j) `max` recur (i, pred j)
```

i文字めとj文字めが等しいとき、その対により長さは1増える。

```haskell
    recur (i, j)
      | as !! pred i == as !! pred j = succ $ recur (pred i, pred j)
```

碁盤の目に、ところどころ対角線が引かれていて、それを通るときスコアが増えるゲームで、
左上の格子点から右下の格子点まで辿るときの最大得点を考える問題と同等であるとわかる。
<!--
i文字めとj文字めが等しいときも、その対応を使わず、別の迂回路からのスコアがベストである可能性を考慮する解説もある。

```haskell
    recur (i, j) = maximum $
                   [succ $ recur (pred i, pred j) | as !! pred i == bs !! pred j] ++
                   [recur (pred i,j), recur (i,pred j)]
```

しかしそのような状況を考慮する必要はない。
（はず。迂回路の途中から、今考えている対角線の出発点に合流する経路が必ず存在する。）
-->
## 動的プログラミングの導入

この `recur` は同じ引数に対する計算を何度も行うため非常に無駄が多い。
特に引数が小さくなった再帰の深いところで顕著になる。
例えば `lcs "ab" "XY"` の計算で `recur` の引数は次のようになる。

```
(2,2) → (1,2), (2,1) → (0,2), (1,1), (1,1), (2,0) → (0,1), (1,0), (0,1), (1,0)
```

つまり、碁盤の目での合流が多く発生する。
この `recur` の結果をメモ化するように、動的プログラミングを導入する。


```haskell
lcs as bs = arr ! (al, bl)
  where
    al = length as
    bl = length bs
    arr = array ((0,0), (al, bl))
          [((i,j), f i j) | i <- [0..al], j <- [0..bl]]
    f 0 _ = 0
    f _ 0 = 0
    f i j
      | a /= b = (arr ! (pred i, j)) `max` (arr ! (i, pred j))
      | a == b = succ $ arr ! (pred i, pred j)
      where
        a = as !! pred i
        b = bs !! pred j
```

## 配列レス実装

配列 `arr` の第0行、第0列は0であり、それ以外の内側の要素の値は上、左、左上の値から決まる。
つまり、全て0である第0行から始めて、次の行の内容を求めるのに必要なものは、
その直前の行の内容だけである。配列全体を記録し続ける必要はない。
これで、配列なしにリストのみで実装できる。

```haskell
lcsLen as bs = last linen
  where
    line0 = 0 : replicate (length as) 0
    linen = foldl step line0 bs
    step line bi = line1
      where
        line1 = 0 : zipWith4 f as line (tail line) line1
        f ai arr00 arr10 arr01
          | ai == bi = succ arr00
          | ai /= bi = max arr10 arr01
```
<!--
対策をしないと、遅延評価により全ての`lines`がサンクとして積み上げられてから`linef`の計算が始まる。これらは結局必要になるので、`linef`に`seq`を仕掛けることで行の計算を強制させている。これをしないとかなり遅くなってしまう。（スペースリーク）
-->

## 共通列の復元

配列による実装では、長さを求めた配列を逆順に辿ることにより共通列の復元を行える。

ここでは、最長となる選択を探すときに同時に、その共通列の候補を記録していく。
最長共通部分列は複数ありうるので、結果はリストのリストとなる。

`max`で最長の候補を選択する際に、長い方の候補を保持する。長さが等しければ両方を維持する。
`ai == bi` の場合は全ての候補に`ai`を続ける。
Haskellのリストの都合で、計算中は逆順に保存する。

```haskell
lcs :: Eq a => [a] -> [a] -> (Int,[[a]])
lcs as bs = fmap (map reverse) $ last linen
  where
    zero = (0,[[]])
    line0 = zero : replicate (length as) zero
    linen = foldl' {-'-} step line0 bs
    step line bi = line1
      where
        line1 = zero : zipWith4 f as line (tail line) line1
        f ai (c00, s00) a10@(c10, s10) a01@(c01, s01)
          | ai == bi = (succ c00, map (ai :) s00)
          | otherwise =
              case compare c10 c01 of
                GT -> a10
                LT -> a01
                EQ -> (c10, nub (s10 ++ s01))
```

最長列が一つでよければ少し簡略化できる。

```haskell
lcs1 :: Eq a => [a] -> [a] -> (Int,[a])
lcs1 as bs = fmap reverse $ last linen
  where
    zero = (0,[])
    line0 = zero : replicate (length as) zero
    linen = foldl' {-'-} step line0 bs
    step line bi = line1
      where
        line1 = zero : zipWith4 f as line (tail line) line1
        f ai (c00, s00) a10 a01
          | ai == bi           = (succ c00, ai : s00)
          | fst a10 >= fst a01 = a10
          | otherwise          = a01
```

完成版を[最長共有部分列](/ad/string/lcs)におく。

## 関連問題

- [DP まとめコンテスト F LCS](https://atcoder.jp/contests/dp/tasks/dp_f) - [ACコード](https://atcoder.jp/contests/dp/submissions/29253805)
- [CODE FESTIVAL 2015 あさぷろ Middle B ヘイホー君と削除](https://atcoder.jp/contests/code-festival-2015-morning-middle/tasks/cf_2015_morning_easy_d) - [ACコード](https://atcoder.jp/contests/code-festival-2015-morning-middle/submissions/23168078)
