---
order: -28000
---
# 28.ワーシャルフロイド法（途中）

参考

- [ワーシャル–フロイド法 Wikipedia](https://ja.wikipedia.org/wiki/%E3%83%AF%E3%83%BC%E3%82%B7%E3%83%A3%E3%83%AB%E2%80%93%E3%83%95%E3%83%AD%E3%82%A4%E3%83%89%E6%B3%95)
- [競技プログラミングで使う有名グラフアルゴリズムまとめ](https://qiita.com/kazukazukazukazu/items/e5db00a3aefb1f5ed72f#5-%E3%83%AF%E3%83%BC%E3%82%B7%E3%83%A3%E3%83%AB%E3%83%95%E3%83%AD%E3%82%A4%E3%83%89%E6%B3%95)

重み付き有向グラフの全ての頂点間の距離を一度に求める。ただし $O(|V|^3)$ である。

pediaの疑似コードから始める。
ノードを1からnの整数で呼ぶ。

- 入力：辺と重み
- 出力：`d[i,j]` iからjへの最良の経路のコスト、なければ無限大

``` #
// 初期化
for i in [1..n]
  for j in [1..n]
    d[i,j] ← iからjへの辺が重みwならw、辺がなければ無限大
-- ここで d[i,j] は、直接の経路の重みが入っている

// 反復計算
for k in [1..n] -- ノードkを経由した経路について、順に考える
  for i in [1..n] -- 始点 i から
    for j in [1..n] -- 終点 j まで
      d[i,j] = min(dij, d[i,k] + d[k,j]) -- kを経由した方が近いなら更新
```

- 重みに関して制限が見当たらないので、負の重みも許すようだ。
- dの初期化は「辺があるとき」なので、言葉通りに受け取ると、dの対角要素 `d[k,k]` は
そのような自己ループがなければ無限大に設定することになる。
- 参考サイトでは、そうしておいてd[k,k]が負になったとき負閉路が存在する（ので結果は不正確？）と判定し、
なおかつその判定の後で全ての `d[k,k]` を0にしてから返している。
- 10行めのjの走査で、j = k の回では `d[i,j] = d[i,k]` の更新が行われる。
これはそれ以降の回での右辺の `d[i,k]` を書き換えることになるので、
配列アクセスを節約する下の変更を行ったプログラムは、上と結果が異なるように見える。
（しかしこれが最終結果に影響を及ぼさない、という話が「アルゴリズムイントロダクション」の演習問題にあったとかなんとか。）

```
for k in [1..n] 
  for i in [1..n]
    dik = d[i,k]
    for j in [1..n]
      d[i,j] = min(dij, dik + d[k,j])
```

9,10行めの反復は、iとj全ての組み合わせに関して行うことだけに意味があるので、順序に意味はない。
現状、最も内側のjのループで、`d[k,*]`の行を読み、`d[i,*]` の行を読み書きするので、
この行がメモリ上で連続して配置される向きに配列をとるべきである。

素朴にHaskell化してみる。`d`は`Array`で表現する。
ノードは1からnの整数、重みも `Int` で、経路がないことを `maxBound` で表す。
グラフは `((出発ノード,到着ノード),重み)` の３項組のリストで与える。

```haskell
import Data.Array
import Data.List

-- @gotoki_no_joe
warshallFloyd :: Int                 -- 頂点数
              -> [(Int,Int,Int)]     -- 辺の重み
              -> Array (Int,Int) Int -- 最小コスト
warshallFloyd n edges = dn
  where
    range = [1..n]
    d0 = accumArray (flip const) maxBound ((1,1),(n,n)) edges
    dn = foldl step d0 range
    step d k = d //
      [((i,j), dikj)
      | i <- range, let dik = d ! (i,k), dik < maxBound
      , j <- range, let dkj = d ! (k,j), dkj < maxBound
      , let dikj = dik + dkj, d ! (i,j) > dikj
      ]
```

Vector版

頂点数Nに対して頂点番号は0～N-1、
グラフは(出頂点,入頂点,重み)のリストで与え、
結果は「出発点、終点を与えるとコストを返す」関数で返される。

```haskell
import Control.Monad
import Control.Monad.ST
import qualified Data.Vector.Unboxed as UV
import qualified Data.Vector.Unboxed.Mutable as MUV

warshallFloydV :: Int                         -- 頂点数
               -> [(Int,Int,Int)]             -- グラフ
               -> (Int -> Int -> Int)         -- 距離
warshallFloydV n graph = reader n $ UV.create action
  where
    reader n v i j = v UV.! (i*n+j)
    action :: ST s (MUV.MVector s Int)
    action = do
      vec <- MUV.replicate (n * n) maxBound
      forM_ graph (\(i,j,w) -> MUV.write vec (i*n+j) w)
      forM_ [0..n-1] (\k ->
        forM_ [0..n-1] (\i -> do
          dik <- MUV.read vec (i*n+k)
          when (dik < maxBound) (
            forM_ [0..n-1] (\j -> do
              dkj <- MUV.read vec (k*n+j)
              when (dkj < maxBound) (MUV.modify vec (min (dik + dkj)) (i*n+j))
              )
            )
          )
        )
      return vec
```

## 関連問題

- [ARC079 D Wall](https://atcoder.jp/contests/abc079/tasks/abc079_d) - [ACコード](https://atcoder.jp/contests/abc079/submissions/28984048) Arrayによる実装  
ダイクストラ法による[別解](https://atcoder.jp/contests/abc079/submissions/28984258)
- [ABC073 D joisino's travel](https://atcoder.jp/contests/abc073/tasks/abc073_d) - 
- (expert!) [ABC074 D Restoring Road Network](https://atcoder.jp/contests/abc074/tasks/arc083_b) - 

他サイト

- (expert!) [yukicoder No.1344 Typical Shortest Path Sum](https://yukicoder.me/problems/no/1344)
