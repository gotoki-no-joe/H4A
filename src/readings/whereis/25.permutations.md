---
order: -25000
---

# 25.短めな順列の作成

itertoolsの累積和以外の機能の紹介。

## ある値が何個あるか

並びに対して、等しいものが連続しているところをまとめる。  
それ`Data.List.group`と`Data.List.groupBy`そのもの。

## 組み合わせ

### 順列

全て異なる要素のリストに対して、その並べ方を全て得る。  
要素が $n$ 個のとき、場合の数は $n!$

順列組み合わせは`Data.List.permutations`でできる。ただし出現順が謎。

要素 $n$ 個から $r$ 個を取り出して並べる並べ方は $_n \textrm{P}_r = \frac{n!}{(n-r)!}$ とおり。  
これを、リストで与えられた並び順を昇順として、順列を辞書順に生成する。

```haskell
-- @gotoki_no_joe
-- 順列を辞書順で
perm xs = permOf (length xs) xs

permOf 0 _ = [[]]
permOf r xs = [a : ys | (a,as) <- one [] xs, ys <- permOf (pred r) as]
  where
    -- 要素を一つ選び、残りの要素を付けて返す
    one bs [] = []
    one bs (a:as) = (a, rev bs as) : one (a:bs) as
    -- reverse bs ++ as
    rev bs as = foldl' {-'-} (flip (:)) as bs
```

```
> perm [1,2,3]
[[1,2,3],[1,3,2],[2,1,3],[2,3,1],[3,1,2],[3,2,1]]
> permOfR 2 [1,2,3]
[[1,2],[1,3],[2,1],[2,3],[3,1],[3,2]]
> Data.List.permutations [1,2,3]
[[1,2,3],[2,1,3],[3,2,1],[2,3,1],[3,1,2],[1,3,2]]
```

### 組み合わせ

全て異なる $n$ 要素のリストから $r$ 個選んで取り出す方法の
場合の数は ${}_n \textrm{C}_r = \frac{n!}{r! (n-r)!}$ とおり。

```haskell
-- @gotoki_no_joe
combOf 0 _      = [[]]
combOf _ []     = [  ]
combOf n (a:as) = [a:bs | bs <- combOf (pred n) as] ++ combOf n as
```

```
> combOf 3 [1..5]
[[1,2,3],[1,2,4],[1,2,5],[1,3,4],[1,3,5],[1,4,5],[2,3,4],[2,3,5],[2,4,5],[3,4,5]]
```

### 直積

要素数 $n$ の集合から $k$ 回要素を選択する方法の場合の数は $n^k$ とおり。

```haskell
-- @gotoki_no_joe
prodOf 0 _  = [[]]
prodOf n as = [a:bs | a <- as, bs <- prodOf (pred n) as]
```

リストはモナドで、リストのモナドはリスト内包表記の生成器と同じく、要素をひとつ選んで以降の計算を全ての場合で行う。  
`sequence` は、モナドの計算のリストが与えられたとき、それを順に実行した結果をリストに集める。  
つまり、 `replicate 3 as` は `[as,as,as]` というリストで、
これをリストモナドとして `sequence` で評価すると、下の式は全て同じ意味になる。

```haskell
sequence (replicate 3 as)
-- Do記法だと
do
  v1 <- as
  v2 <- as
  v3 <- as
  return [v1,v2,v3]
-- 内包表記だと
[[v1,v2,v3] | v1 <- as, v2 <- as, v3 <- as]
```

つまり

```haskell
prodOf n as = sequence $ replicate n as
```

## nextPermutation

`Data.Vector.Mutable.nextPermutation` は、順列の入ったベクタを書き変えて、辞書順で次の順列にする。
末尾の場合にはFalseが返る。
（どこかで再実装したと思う。）

## 関連問題

- [ABC028 C 数を3つ選ぶマン](https://atcoder.jp/contests/abc028/tasks/abc028_c) - [ACコード](https://atcoder.jp/contests/abc028/submissions/23051091) 組み合わせの場合を列挙
- [ABC183 C Travel](https://atcoder.jp/contests/abc183/tasks/abc183_c) - [ACコード](https://atcoder.jp/contests/abc183/submissions/23056456) 順列
